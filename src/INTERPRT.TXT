10 '   P-CODE INTERPRETER FOR MINI-PASCAL COMPILER MK 1
20 '   WRITTEN BY C.TAYLOR 1988
30 '   FOR MICROSOFT-BASIC COMPUTERS WITH AT LEAST ONE DISK DRIVE
40 DIM C(1000,3):'    ARRAY TO HOLD P-CODE
50 DIM S(1000):'    STACK
60 DIM D(20):'    DISPLAY USED FOR ACCESSING DYNAMICALLY ALLOCATED VARIABLES
70 PRINT "P-CODE INTERPRETER MK 1"
80 INPUT "NAME OF FILE TO BE INTERPRETED";F$
90 INPUT "NUMBER OF DISK DRIVE ON WHICH FILE RESIDES ";F
100 F$ = F$ + ":" + CHR$ (F + 48)
110 OPEN "I",1,F$:'   OPEN INPUT FILE
120 INPUT #1,OP,P1,P2:'   READ OPCODE, PARAMETER 1 AND PARAMETER 2 FROM INPUT FILE
130 IF   OP = 19 THEN C(P1,3) = P2: GOTO 120:'   BACKPATCH JUMPS
140 IF   OP = 20 THEN GOTO 160
150 C(PC,1) = OP:C(PC,2) = P1:C(PC,3) = P2:PC = PC + 1: GOTO 120
160 C(PC,1) = 20:C(PC,2) = 0:C(PC,3) = 0: CLOSE 1:'   CLOSE INPUT FILE
170 INPUT "READY TO INTERPRET ";A$
180 PC = 0:T = 0:B = 1:S(1) = 1:S(2) = 0:S(3) = -1:D(1) = 1
190 OP = C(PC,1):P1 = C(PC,2):P2 = C(PC,3)
200 PC = PC + 1:IX = 0
210 IF   OP > 8 THEN IX =1:OP = OP - 10
220 IF   OP = 0 THEN T = T + 1:S(T) = P2: GOTO 190
230 IF   OP <> 1 THEN 440
240 IF   P2 = 0 THEN T = B - 1:B = S(T + 2):PC = S(T + 3):LV = S(T + 1):D(LV) = B: GOTO 190
250 IF   P2 = 1 THEN S(T) = - S(T): GOTO 190
260 IF   P2 > 5 THEN 300
270 T = T - 1
271 IF   P2 = 5 THEN S(T) = INT (S(T) / S(T + 1)): GOTO 280
272 S(T) = INT ( - ((P2 = 2) * (S(T) + S(T + 1)) + (P2 = 3) * (S(T) - S(T + 1)) + (P2 = 4) * (S(T) * S(T + 1))))
280 GOTO 190
300 IF   P2 = 6 THEN S(T) = S(T) AND 1: GOTO 190
310 IF   P2 > 13 THEN 390
320 T = T -1
330 S(T) = ((P2=8)*(S(T)=S(T+1))+(P2=9)*(S(T)<>S(T+1))+(P2=10)*(S(T)<S(T+1))+(P2=11)*(S(T)>=S(T+1))+(P2=12)*(S(T)>S(T+1))+(P2=13)*(S(T)<=S(T+1))+(P2=14)*-(S(T) OR S(T+1))+(P2=15)*-(S(T) AND S(T+1)))
340 GOTO 190
390 IF   P2 = 16 THEN S(T) = NOT S(T): GOTO 190
400 IF   P2 > 20 THEN 420
410 T = - ((P2 = 19) * (T + 1) + (P2 = 20) * (T - 1)): GOTO 190
420 IF   P2 = 21 THEN T = T + 1:S(T) = S(T - 1): GOTO 190
440 IF   OP < > 2 THEN 470
450 P1 = P1 + (S(T) * - (IX = 1))
460 T = T + 1 - IX:S(T) = S(D(P2) + P1): GOTO 190
470 IF   OP < > 3 THEN 500
480 P1 = P1 + (S(T - 1) * - (IX = 1))
490 S(D(P2) + P1) = S(T):T = T - IX - 1: GOTO 190
500 IF   OP < > 4 THEN 520
510 S(T + 2) = B:S(T + 3) = PC:S(T + 1) = P1:LV = P1:B = T + 1:D(LV) = B:PC = P2: GOTO 190
520 IF   OP = 5 THEN T = T + P2: GOTO 190
530 IF   OP = 6 THEN PC = P2: GOTO 190
535 IF   OP = 10 THEN PRINT : PRINT "INTERPRETATION ENDED": STOP
540 IF   OP < > 7 THEN 570
550 IF   S(T) = P1 THEN PC = P2
560 T = T - 1: GOTO 190
570 IF   P2 = 0 THEN T = T + 1: INPUT A$:S(T) = ASC (A$): GOTO 190:'   ASC CONVERTS FIRST CHARACTER IN A$ TO ASCII NUMBER
580 IF   P2 = 1 THEN PRINT CHR$ (S(T)):T = T - 1: GOTO 190:'    CHR$ CONVERTS ASCII NUMBER IN S(T) TO A STRING CHARACTER
590 IF   P2 = 2 THEN T = T + 1: INPUT A:S(T) = A * - (A < 32768 AND A > -32767): GOTO 190
600 PRINT S(T):T = T - 1: GOTO 190